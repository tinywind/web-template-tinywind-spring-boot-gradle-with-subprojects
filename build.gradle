buildscript {
    def env = { String name -> System.getProperty(name, System.getenv(name)) }

    ext.databaseHost = env("JDBC_HOST") ?: "localhost"
    ext.databasePort = env("JDBC_POST") ?: 1521
    ext.databaseUsername = env("JDBC_USERNAME") ?: "database_username"
    ext.databasePassword = env("JDBC_PASSWORD") ?: "database_password"

    ext.databaseName = env("JDBC_DATABASE") ?: "orcl"
    ext.databaseUrl = "jdbc:oracle:thin:@$databaseHost:$databasePort:$databaseName"

    ext {
        springBootVersion = '2.3.0.RELEASE'
        springCloudVersion = 'Hoxton.SR4'
        vavrVersion = '0.10.2'
        poiVersion = '4.1.0'
    }
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.72"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.9.RELEASE"
        classpath "io.freefair.gradle:lombok-plugin:5.0.1"
    }
}

allprojects {
    group 'kr.co.klnet'
    version '0.1-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'idea'
    apply plugin: "io.freefair.lombok"
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava.options.encoding = "UTF-8"
    compileTestJava.options.encoding = "UTF-8"

    repositories {
        mavenCentral()
    }

    configurations {
        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    test {
        useJUnitPlatform()
    }

    dependencies {
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        testApi('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }

        implementation 'org.jetbrains.kotlin:kotlin-reflect'
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core'
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk8'

        testImplementation "org.jetbrains.kotlin:kotlin-test"
        testImplementation "org.jetbrains.kotlin:kotlin-test-junit"

        implementation 'org.slf4j:slf4j-api'
        implementation 'ch.qos.logback:logback-classic'

        implementation 'org.apache.commons:commons-lang3:3.8.1'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }
}

project(':meta') {
    bootJar.enabled = false
    jar.enabled = true

    dependencies {
        api files('libs/ojdbc6.jar')
    }
}

project(':spring-error-notifier') {
    bootJar.enabled = false
    jar.enabled = true

    dependencies {
        implementation 'net.rakugakibox.util:yaml-resource-bundle:1.1'

        implementation 'org.springframework:spring-webmvc'
        implementation 'org.springframework.boot:spring-boot'
        implementation 'org.springframework.boot:spring-boot-actuator-autoconfigure'
        implementation 'com.fasterxml.jackson.core:jackson-databind'
        implementation 'eu.bitwalker:UserAgentUtils:1.21'
        implementation 'com.squareup.okhttp3:okhttp:4.7.2'

        compileOnly 'javax.servlet:javax.servlet-api'
    }
}

project(':common-spring') {
    bootJar.enabled = false
    jar.enabled = true

    dependencies {
        implementation 'net.rakugakibox.util:yaml-resource-bundle:1.1'

        api 'org.springframework:spring-webmvc'
        implementation 'org.springframework.boot:spring-boot'
        implementation 'org.springframework.boot:spring-boot-actuator-autoconfigure'
        implementation 'com.fasterxml.jackson.core:jackson-databind'
        implementation 'eu.bitwalker:UserAgentUtils:1.21'
        api "io.vavr:vavr:${vavrVersion}"
        api "io.vavr:vavr-jackson:${vavrVersion}"
        implementation 'com.squareup.okhttp3:okhttp:4.7.2'

        compileOnly 'javax.servlet:javax.servlet-api'
    }
}

project(':webapp') {
    apply plugin: 'war'

    dependencies {
        compile project(':meta')
        compile project(':common-spring')
        compile project(':spring-error-notifier')

        implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.1.3'
        implementation 'org.springframework.boot:spring-boot-starter-jdbc'
        implementation 'org.springframework.boot:spring-boot-starter-aop'
        implementation 'org.springframework.boot:spring-boot-starter-validation'

        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'io.micrometer:micrometer-registry-prometheus'

        implementation 'org.springframework.boot:spring-boot-starter-mail'
        implementation 'org.thymeleaf:thymeleaf:3.0.11.RELEASE'


        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.apache.tomcat.embed:tomcat-embed-jasper'
        implementation 'taglibs:standard:1.1.2'
        implementation 'javax.servlet:jstl'

        implementation 'io.springfox:springfox-swagger2:2.8.0'
        implementation 'io.springfox:springfox-swagger-ui:2.8.0'

        implementation 'org.apache.httpcomponents:httpcore:4.4.4'
        implementation 'org.apache.httpcomponents:httpclient:4.5.6'
        implementation 'org.mindrot:jbcrypt:0.3m'
        implementation 'commons-fileupload:commons-fileupload:1.3.2'
        implementation 'commons-io:commons-io:2.6'
        implementation "org.apache.poi:poi:${poiVersion}"
        implementation "org.apache.poi:poi-ooxml:${poiVersion}"

        implementation 'org.webjars:jquery:3.4.1'
        implementation 'org.webjars:jquery-blockui:2.65'
        implementation 'org.webjars:momentjs:2.21.0'
        implementation 'org.webjars:d3js:5.9.1'
        implementation 'org.webjars.bower:less.js:2.7.3'
        implementation 'org.webjars:jquery-ui:1.12.1'

        implementation 'org.bgee.log4jdbc-log4j2:log4jdbc-log4j2-jdbc4.1:1.16'
    }
}
